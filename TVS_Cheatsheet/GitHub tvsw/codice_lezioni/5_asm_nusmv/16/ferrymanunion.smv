-- con uso di define e union
MODULE main
VAR
	wolf : {L, R};
	goat : {L, R};
	cabbage : {L, R};
	ferryman : {L, R};
	carry : {w, g, c, 0}; -- cosa trasportero' in questo stato per il prossimo
DEFINE
	-- definisce gli stati di pericolo
	pericolo := goat != ferryman & (wolf = goat | cabbage = goat);
	-- oppure 
	safe := ((goat = wolf | goat = cabbage) -> goat = ferryman);
	-- definisce la soluzione
	soluzione := goat = R & wolf = R & cabbage = R;
ASSIGN
	init (wolf) := L;
	init (goat) := L;
	init (cabbage) := L;
	init (ferryman) := L;
	-- ferryman
	next (ferryman) :=
		case
			ferryman = L : R;
			ferryman = R : L;
		esac;
		-- wolf 
	next (wolf) :=
		case
			carry = w : next (ferryman);
			TRUE : wolf;
		esac;
		-- goat 
	next (goat) :=
		case
			carry = g : next (ferryman);
			TRUE : goat;
		esac;
		-- cabbage 
	next (cabbage) :=
		case
			carry = c : next (ferryman);
			TRUE : cabbage;
		esac;
		-- cosa può trsportare (senza preocciparsi dei problemi)
	carry :=
		case -- wolf
			ferryman = wolf : {w, 0};
			TRUE : 0;
		esac union
		case
			ferryman = goat : {g, 0};
			TRUE : 0;
		esac union
		case
			ferryman = cabbage : {c, 0};
			TRUE : 0;
		esac;

		-- FALSE che uso per controllare che la speficia contenga alcuni comportamenti
	-- il wolf non può andare mai a destra (mi aspetto che sia falsa)
CTLSPEC AG (wolf != R);
-- lo stesso per goat
CTLSPEC AG (goat != R);
-- non sucded mai che tutti sono a R
CTLSPEC AG (! (goat = R & wolf = R & cabbage = R));

-- comeunqu vada prima o poi sono tutti a destra
CTLSPEC AF (goat = R & wolf = R & cabbage = R);

CTLSPEC AG(!pericolo);

CTLSPEC !EF(soluzione);


-- TRUE che uso per controllare che la spec sia corretta
-- tutti sulla sponda destra
CTLSPEC EF (goat = R & wolf = R & cabbage = R);


-- quello sopra non basta perchè potrebbe portarmi ad unostato rischioso
-- voglio provar einvece questo:
-- è possibile portare a destra tutti senza mai lasciare il wolf insieme al goat
-- e il goat insieme al cabbage (se non c'è il ferry man)

--CTLSPEC E[ (!pericolo) U soluzione];

CTLSPEC E[ safe U soluzione];
