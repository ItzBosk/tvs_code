
MODULE main
VAR
	doors : {CLOSED, CLOSING, OPEN, OPENING}; --controlled
	gears : {EXTENDED, EXTENDING, RETRACTED, RETRACTING}; --controlled
	handle : {DOWN, UP}; --monitored
ASSIGN
	init (doors) := CLOSED;
	init (gears) := EXTENDED;
	-- doors
	next (doors) :=
		case
			handle = UP : -- retracting sequence

				case
					gears = RETRACTED : -- if already retracted, close dooors

						case -- close doors
							doors = OPEN : CLOSING;
							doors = CLOSING : CLOSED;
							doors = OPENING : CLOSING;
							doors = CLOSED : CLOSED;
						esac;
					TRUE : --gears != RETRACTED
					-- if not retracted open doors if necessary

						case
							doors = CLOSED : OPENING;
							doors = CLOSING : OPENING;
							doors = OPENING : OPEN;
							doors = OPEN : OPEN;
						esac;
				esac;
			TRUE : -- handle = DOWN
			-- extending sequence

				case
					gears = EXTENDED :
					-- if already extened close doors

						case -- close doors
							doors = OPEN : CLOSING;
							doors = CLOSING : CLOSED;
							doors = OPENING : CLOSING;
							doors = CLOSED : CLOSED;
						esac;
					TRUE :
					-- not extended, open doors if necessary

						case
							doors = CLOSED : OPENING;
							doors = OPENING : OPEN;
							doors = CLOSING : CLOSING;
							doors = OPEN : OPEN;
						esac;
				esac;
		esac;
		-- gears		
	next (gears) :=
		case
			handle = UP & gears != RETRACTED & doors = OPEN: -- retracting sequence
				case -- retract gears
					gears = EXTENDED : RETRACTING;
					gears = EXTENDING : RETRACTING;
					gears = RETRACTING : RETRACTED;
					gears = RETRACTED : RETRACTED;
				esac;
			handle = DOWN & gears != EXTENDED & doors = OPEN:
				case -- retract gears
					gears = RETRACTING : EXTENDING;
					gears = RETRACTED : EXTENDING;
					gears = EXTENDING : EXTENDED;
					gears = EXTENDED : EXTENDED;
				esac;
			TRUE : gears;
		esac;
--CTL properties
CTLSPEC AG(((AG((handle = DOWN)) & ((gears = EXTENDED) & (doors = CLOSED))) -> AG(((gears = EXTENDED) & (doors = CLOSED)))));
CTLSPEC AG(((((doors = CLOSED) & (gears = RETRACTED)) & AG((handle = UP))) -> AG(((doors = CLOSED) & (gears = RETRACTED)))));
CTLSPEC AG((((gears = EXTENDING) & (handle = DOWN)) -> AX((gears = EXTENDED))));
CTLSPEC AG((((handle = UP) & (gears = EXTENDING)) -> AX((gears = RETRACTING))));
CTLSPEC AG((((handle = UP) & (gears = RETRACTING)) -> AX((gears = RETRACTED))));
CTLSPEC AG((((gears = RETRACTING) & (handle = DOWN)) -> AX((gears = EXTENDING))));
CTLSPEC AG(((((doors = CLOSING) & (gears = EXTENDED)) & (handle = DOWN)) -> AX((doors = CLOSED))));
CTLSPEC AG((((handle = UP) & ((doors = CLOSING) & (gears = EXTENDED))) -> AX((doors = OPENING))));
CTLSPEC AG(((((doors = OPENING) & (gears = RETRACTED)) & (handle = DOWN)) -> AX((doors = OPEN))));
CTLSPEC AG((((handle = UP) & ((doors = OPENING) & (gears = RETRACTED))) -> AX((doors = CLOSING))));
CTLSPEC AG((AG((handle = DOWN)) -> AF(((gears = EXTENDED) & (doors = CLOSED)))));
CTLSPEC AG((AG((handle = UP)) -> AF(((doors = CLOSED) & (gears = RETRACTED)))));
CTLSPEC AG((AG((handle = DOWN)) -> AX(AG((gears != RETRACTING)))));
CTLSPEC AG((AG((handle = UP)) -> AX(AG((gears != EXTENDING)))));
--AsmetaL invariants
CTLSPEC	AG((((gears = EXTENDING) | (gears = RETRACTING)) -> (doors = OPEN)));
CTLSPEC	AG(((doors = CLOSED) -> ((gears = EXTENDED) | (gears = RETRACTED))));
		
		